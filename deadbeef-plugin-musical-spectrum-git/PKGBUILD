# Maintainer: RaphaÃ«l Doursenaud <rdoursenaud@free.fr>
pkgbase=deadbeef-plugin-musical-spectrum-git
pkgname=(deadbeef-plugin-musical-spectrum-gtk3-git)
pkgver=r292.f177bdd
pkgrel=1
_pkgdesc="Musical spectrum for the DeaDBeeF audio player"
url="https://github.com/cboxdoerfer/ddb_musical_spectrum"
arch=('x86_64')
license=('GPL2')
makedepends=('git' 'pkg-config' 'deadbeef' 'fftw' 'gtk3')

_gitname=ddb_musical_spectrum
_gitroot=https://github.com/cboxdoerfer/${_gitname}

source=("git+${_gitroot}")
sha512sums=('SKIP')

pkgver() {
	cd "${srcdir}/${_gitname}"
	printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
	cd "${_gitname}"

	sed -i 's/\(spectrum_wavedata_listener (void \*ctx,\) \(ddb_audio_data_t\)/\1 const \2/' spectrum.c
	make gtk3
}

package_deadbeef-plugin-musical-spectrum-gtk3-git() {
	pkgdesc="${_pkgdesc} GTK3"
	depends=('deadbeef' 'fftw' 'gtk3')
	provides=('deadbeef-plugin-musical-spectrum-gtk3')
	conflicts=('deadbeef-plugin-musical-spectrum-gtk3')
	install -D -v -c "${srcdir}/${_gitname}/gtk3/ddb_vis_musical_spectrum_GTK3.so" "${pkgdir}/usr/lib/deadbeef/ddb_vis_musical_spectrum_GTK3.so"
}
