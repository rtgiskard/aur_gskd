# Maintainer: Erik Reider <erik.reider@protonmail.com>
pkgname=swaync-git
_pkgname=swaync
pkgver=0.11.2.r0.77199cd
pkgrel=1
pkgdesc="A simple notification daemon with a customizable GTK panel"
url="https://github.com/rtgiskard/SwayNotificationCenter"
arch=(
    'x86_64'
    'aarch64' # ARM v8 64-bit
    'armv7h'  # ARM v7 hardfloat
)
license=('GPL3')
depends=("gtk4" "gtk4-layer-shell" "dbus" "glib2" "gobject-introspection" "libgee" "json-glib" "libpulse" "gvfs" "libnotify" "granite7" "blueprint-compiler" "libadwaita")
conflicts=("swaync" "swaync-client")
provides=("swaync" "swaync-client" "notification-daemon")
makedepends=("vala>=0.56" meson git scdoc sassc)
source=("$_pkgname::git+$url")
sha256sums=('SKIP')

pkgver() {
    git -C $_pkgname describe --long --tags | sed 's/^v//;s/\([^-]*-\)g/r\1/;s/-/./g'
}

prepare() {
    cd $_pkgname
    git checkout dev
}

build() {
    # Very important to be in the directory! Otherwise somehow breaks linking to the .ui files
    cd $_pkgname
    arch-meson build -Dscripting=true
    ninja -C build
}

package() {
    cd $_pkgname
    DESTDIR="$pkgdir/" ninja -C build install
    install -Dm644 "COPYING" -t "$pkgdir/usr/share/licenses/$pkgname"
    install -Dm644 "README.md" -t "$pkgdir/usr/share/doc/$pkgname"
}
