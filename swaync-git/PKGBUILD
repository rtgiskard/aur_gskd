# Maintainer: Erik Reider <erik.reider@protonmail.com>
pkgname=swaync-git
_pkgname=swaync
pkgver=0.10.1.r47.8a4825c
pkgrel=1
pkgdesc="A simple notification daemon with a customizable GTK panel"
url="https://github.com/rtgiskard/SwayNotificationCenter"
arch=(
	'x86_64'
	'aarch64' # ARM v8 64-bit
	'armv7h'  # ARM v7 hardfloat
)
license=('GPL3')
depends=("gtk3" "gtk-layer-shell" "dbus" "glib2" "gobject-introspection" "libgee" "json-glib" "libhandy" "libpulse" "gvfs" "libnotify" "granite")
conflicts=("swaync" "swaync-client")
provides=("swaync" "swaync-client" "notification-daemon")
makedepends=("vala>=0.56" meson git scdoc sassc)
source=("$_pkgname::git+$url")
sha256sums=('SKIP')

pkgver() {
	git -C $_pkgname describe --long --tags | sed 's/^v//;s/\([^-]*-\)g/r\1/;s/-/./g'
}

prepare() {
	cd $_pkgname
	git checkout main
}

build() {
	arch-meson $_pkgname build -Dscripting=true
	ninja -C build
}

package() {
	DESTDIR="$pkgdir/" ninja -C build install
	install -Dm644 "$_pkgname/COPYING" -t "$pkgdir/usr/share/licenses/$pkgname"
	install -Dm644 "$_pkgname/README.md" -t "$pkgdir/usr/share/doc/$pkgname"
}
