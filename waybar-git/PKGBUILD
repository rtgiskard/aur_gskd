# Maintainer: Alexis Rouillard <contact@arouillard.fr>

pkgname=waybar-git
pkgver=0.10.4.r6.003dd3a9
pkgrel=1
pkgdesc='Highly customizable Wayland bar for Sway and Wlroots based compositors (GIT)'
arch=('x86_64')
url='https://github.com/Alexays/Waybar/'
license=('MIT')
provides=('waybar')
conflicts=('waybar')
depends=(
	'gtkmm3'
	'libjsoncpp.so'
	'libsigc++'
	'fmt'
	'jack' 'libjack.so'
	'wayland'
	'libspdlog.so'
	'gtk-layer-shell'
	'libupower-glib.so'
	'upower'
	'libevdev'
	'libinput'
	'libpulse'
	'libnl'
	'libappindicator-gtk3'
	'libdbusmenu-gtk3'
	'libmpdclient'
	'libsndio.so'
	'libxkbcommon'
	'libwireplumber'
	'playerctl'
)
makedepends=(
	'git'
	'cmake'
	'catch2'
	'meson'
	'scdoc' # For generating manpages
	'wayland-protocols'
)
optdepends=(
	'otf-font-awesome: Icons in the default configuration'
)

source=("${pkgname}::git+https://github.com/rtgiskard/Waybar#branch=dev")

sha1sums=('SKIP')

pkgver() {
	git -C "${srcdir}/${pkgname}" describe --long --tags | sed 's/^v//;s/\([^-]*-\)g/r\1/;s/-/./g'
}

prepare() {
	cd "${srcdir}/${pkgname}"
}

build() {
	cd "${srcdir}/${pkgname}"
	rm -rf "${srcdir}/build"
	meson setup \
		--prefix=/usr \
		--buildtype=plain \
		--auto-features=enabled \
		-Dexperimental=true \
		-Dcava=disabled \
		-Dtests=disabled \
		"${srcdir}/build"
	ninja -C "${srcdir}/build"
}

package() {
	DESTDIR="$pkgdir" ninja -C "${srcdir}/build" install
	install -Dm644 "${srcdir}/${pkgname}/LICENSE" -t "$pkgdir/usr/share/licenses/$pkgname/"
}
