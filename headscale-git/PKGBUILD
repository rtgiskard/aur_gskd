# Maintainer: giskard <rtgiskard@gmail.com>

_pkgname=headscale
pkgname="${_pkgname}-git"
pkgver=0.22.1.r17.ga2b7608
pkgrel=1
pkgdesc="An open source, self-hosted implementation of the Tailscale coordination server."
arch=('x86_64')
url="https://github.com/juanfont/headscale"
license=('BSD')
depends=()
makedepends=('git' 'go')
optdepends=(
	'wireguard-tools: CLI tools for generating keys'
	'sqlite: default databse provider'
	'postgresql: alternative database provider'
	'tailscale: the client'
)
provides=("${_pkgname}")
conflicts=("${_pkgname}")
source=(
	"${_pkgname}::git+https://github.com/juanfont/headscale.git"
	'headscale.service'
	'headscale.sysusers'
	'headscale.tmpfiles'
)
sha256sums=('SKIP'
            '7fff615806605cda9954b760a1466367a4603a9072139c221fdc98908e3ec006'
            'bc798d97691ec660e45f40a10c92b7283e56a33c312a93a0ffe02337f0feec10'
            '2f3c7e8b8161698404edf52a56f55e7472d483b8739eb8cda1885c9bacc6f1a6')

pkgver() {
	cd "${srcdir}/${_pkgname}"
	git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g;s/^v//'
}

build() {
	cd "${srcdir}/${_pkgname}"
	GOOS=linux CGO_ENABLED=0 go build \
		-trimpath -buildmode=pie -tags ts2019 \
		-ldflags="-s -w -X github.com/juanfont/headscale/cmd/headscale/cli.Version=$pkgver" \
		-a ./cmd/headscale

	# generate bash completion config
	./${_pkgname} completion bash > headscale.completion
}

package() {
	cd "$srcdir/${_pkgname}"
	install -Dm755 "${_pkgname}" "${pkgdir}/usr/bin/${_pkgname}"
	install -Dm644 "headscale.completion" "${pkgdir}/usr/share/bash-completion/completions/${_pkgname}"
	install -Dm644 "config-example.yaml" "${pkgdir}/etc/${_pkgname}/config-example.yaml"
	install -Dm644 "derp-example.yaml" "${pkgdir}/etc/${_pkgname}/derp.yaml"
	install -Dm644 "LICENSE" "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"

	install -Dm644 "${srcdir}/headscale.service" "${pkgdir}/etc/systemd/system/${_pkgname}.service"
    install -Dm644 "${srcdir}/headscale.sysusers" "${pkgdir}/usr/lib/sysusers.d/${_pkgname}.conf"
    install -Dm644 "${srcdir}/headscale.tmpfiles" "${pkgdir}/usr/lib/tmpfiles.d/${_pkgname}.conf"

	install -dm755 "${pkgdir}/var/lib/${_pkgname}"
}
