# Maintainer: Harish Rajagopal <harish dot rajagopals at gmail dot com>

_pkgname=regreet
pkgname="greetd-$_pkgname-git"
pkgver=0.1.1.r28.562dd68
pkgrel=1
pkgdesc="Clean and customizable greeter for greetd"
arch=('x86_64')
url="https://github.com/rtgiskard/ReGreet"
license=(GPL3)
source=("$_pkgname::git+$url.git")
sha256sums=('SKIP')
makedepends=(cargo git)
depends=(greetd gtk4)
provides=(greetd-greeter)
conflicts=("greetd-$_pkgname")
replaces=(regreet-git)

pkgver() {
	cd "$_pkgname"
	printf "%s" "$(git describe --long | sed 's/\([^-]*-\)g/r\1/;s/-/./g')"
}

prepare() {
	cd "$_pkgname"
	cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

build() {
	cd "$_pkgname"

	export RUSTUP_TOOLCHAIN=stable
	export GREETD_CONFIG_DIR="/etc/greetd"
	export CACHE_DIR="/var/cache/${_pkgname}"
	export LOG_DIR="/var/log/${_pkgname}"
	export SESSION_DIRS="/usr/share/xsessions:/usr/share/wayland-sessions"
	cargo build --frozen --release --all-features --target-dir=target
}

package() {
	cd "$_pkgname"
	install -Dm0755 -t "$pkgdir/usr/bin/" "target/release/$_pkgname"
	install -Dm0644 -t "$pkgdir/usr/share/doc/$_pkgname/" "data/$_pkgname.sample.toml"
	install -Dm0644 -t "$pkgdir/usr/share/doc/$_pkgname/" "data/$_pkgname.sample.css"
	install -Dm0644 "data/systemd-tmpfiles.conf" "$pkgdir/usr/lib/tmpfiles.d/$_pkgname.conf"
}
